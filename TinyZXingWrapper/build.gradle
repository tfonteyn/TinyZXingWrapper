plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
}

/*
    $HOME/.gradle/gradle.properties:
        TinyZXingWrapper.properties=$HOME/blah/blah/TinyZXingWrapper.properties

    $HOME/blah/blah/TinyZXingWrapper.properties:
        sign.storeFile=$HOME/blah/blah/keystore.jks
        sign.storePassword=MyStorePassword
        sign.keyAlias=MyKeyAlias
        sign.keyPassword=MyKeyPassword
 */
final def props = new Properties()
if (project.hasProperty("TinyZXingWrapper.properties")) {
    final def file = new File(project.property("TinyZXingWrapper.properties"))
    if (file.exists()) {
        props.load(new FileInputStream(file))
    }
}
kotlin {
    jvmToolchain rootProject.ext.toolChain
}

ext {
    // build version for the library
    tinyZXingWrapperVersion = "1.0.1"
}

android {
    namespace = "com.hardbacknutter.tinyzxingwrapper"
    compileSdk = rootProject.ext.compileSdkVersion

    resourcePrefix("tzw_")

    signingConfigs {
        release {
            storeFile file(props["sign.storeFile"])
            storePassword props["sign.storePassword"]
            keyAlias props["sign.keyAlias"]
            keyPassword props["sign.keyPassword"]
        }
    }

    defaultConfig {
        minSdk = rootProject.ext.minSdkVersion
        targetSdk = rootProject.ext.targetSdkVersion

        signingConfig(signingConfigs.release)

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
    }

    buildTypes {
        debug {

        }
        release {
            minifyEnabled = false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro")
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_17

        coreLibraryDesugaringEnabled = true
    }

    kotlinOptions {
        jvmTarget = rootProject.ext.toolChain
    }

    buildFeatures {
        buildConfig = true
    }

    lint {
        abortOnError = false
    }

    libraryVariants.configureEach {
        final variant ->
            variant.outputs.configureEach {
                outputFileName = "${project.name}-${variant.name}-${tinyZXingWrapperVersion}.aar"
            }
    }
}

dependencies {
    api("com.google.zxing:core:${zxingCoreVersion}")

    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs_nio:${desugarVersion}")

    implementation("androidx.activity:activity-ktx:${androidxActivityVersion}")
    implementation("androidx.fragment:fragment-ktx:${androidxFragmentVersion}")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:${androidxLifecycleVersion}")
    implementation("androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycleVersion")

    implementation("androidx.camera:camera-core:${androidxCardviewVersion}")
    implementation("androidx.camera:camera-camera2:${androidxCardviewVersion}")
    implementation("androidx.camera:camera-lifecycle:${androidxCardviewVersion}")
    implementation("androidx.camera:camera-view:${androidxCardviewVersion}")
    implementation("androidx.camera:camera-extensions:${androidxCardviewVersion}")

    // https://github.com/material-components/material-components-android/releases
    implementation("com.google.android.material:material:${googleMaterialVersion}")
}

tasks.register("javadocGenerate", Javadoc) {
    description "Generates Javadoc for the TinyZXingWrapper library"
    source = project.android.sourceSets.main.java.srcDirs
    options {
        setMemberLevel(JavadocMemberLevel.PUBLIC)
        links("https://developer.android.com/reference/")
    }
    // android.jar
    classpath += project.files(project.android.getBootClasspath())
    // androidx (and other) dependencies
    classpath += files(android.libraryVariants.collect { final variant ->
        variant.getJavaCompileProvider().get().classpath.files
    })
    failOnError false
}

tasks.register('javadocJar', Jar) {
    description = "Creates a Javadoc Jar for the TinyZXingWrapper library." +
            "The result can be found in 'build/outputs/docs/"
    dependsOn javadocGenerate
    from javadocGenerate.destinationDir
    // create a subfolder in the jar for all the html files
    into("docs")
    // Where to store the jar file
    destinationDirectory = project.layout.buildDirectory.dir("outputs/docs")
    archiveFileName = "${project.name}-docs-${tinyZXingWrapperVersion}.jar"
}
